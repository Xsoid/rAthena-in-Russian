//===== rAthena Script =======================================
//= Kafra Functions
//===== By: ==================================================
//= rAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//= L0ne_W0lf
//===== Current Version: =====================================
//= 6.8
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//=
//= Kafra's will need a lot of work. The Welcome Message argument
//= may become obsolete, as most kafras have a slightly differing
//= Welcome message.
//===== Additional Comments: =================================
//= 1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//=     Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Luoyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Luoyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=     to block Kafra Password, read comments at 350 line
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=     Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress.
//=     Need all coords for all guides.. and somehow pass their coords to kafra.
//=     Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= 5.5 Added proper Niflheim welcome message.	[L0ne_W0lf]
//=     Cleaned up the menus a bit. Got rid of the proceeding "-" prefix.
//= 5.5b Missed a preceeding hyphen, which was screwing up teleporting. [L0ne_W0lf]
//= 5.6 Further dialog updates, added  dynamic costs for cart and storage use. [L0ne_W0lf]
//=     Updated some functions to handle Guild Kafras the way they should.
//= 5.6a Corrected a few wrong names in the teleport function. [L0ne_W0lf]
//= 5.7 Correced end message no longer displaying, and the duplicate names [L0ne_W0lf]
//=     Thanks to Barron-Monster for pointing out the issues.
//= 5.8 Fixed another double name being shown when "saving" [L0ne_W0lf]
//= 5.9 Fixed the Kafra Welcome message for guilds. Thanks Barron-Monster. [L0ne_W0lf]
//= 6.0 Closed Kafra Password exploit in Cool Event Corp. Storages until we got a client/packet based password support.
//=     Added 2 args into F_SetKafCode to fit it in Cool Evnt Co. NPC [Lupus]
//= 6.1 Added menu for Turbo Track Kafra Staff. [L0ne_W0lf]
//= 6.2 Updated/Fixed warp cords. [Kisuka]
//= 6.3 #kafra_code is now stored as is. [brianluau]
//= 6.4 Removed unofficial Guide function. [Daegaladh]
//= 6.5 Optimized. [Joseph]
//= 6.6 Added Warp Points / View Points Function. [Joseph]
//= 6.7 Added Rune Knight Kafra. [Euphy]
//= 6.8 Updated some functions and added VIP features. [Euphy]
//============================================================
//= Translated en -> ru [Akaineko] 
//= Upgraded [Akaineko]
//============================================================

//=== Main Function ========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//=   arg(3): Cost to use Storage
//=   arg(4): Cost to Rent a Pushcart
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	.@welcome = getarg(0);
	.@menu_num = getarg(1);

	// Display Kafra Welcome Message
	switch(.@welcome) {
	default:// Default message (obsolete)
		mes "[Сотрудница Кафры]";
		mes "Добро пожаловать. ";
		mes "Корпорация Кафра ";
		mes "всегда к Вашим услугам. ";
		mes "Чем могу помочь?";
		break;
	case 1:// Niflheim specific message
		mes "[Сотрудница Кафры]";
		mes "^666666Д-д-доб-бр-р-ро-";
		mes "пож-жа-алова-а-ать...";
		mes "в Корп-порацию ";
		mes "Ка-а-аффрррра......^000000";
		break;
	case 2:// Guild Castle Kafra message
		mes "[Сотрудница Кафры]";
		mes "Добро пожаловать, член гильдии ^ff0000" + getguildname( getcharid(2) ) + "^000000.";
		mes "Корпорация Кафра всегда рядом с Вами.";
		break;
	case 3:// Amatsu specific message (obsolete)
		mes "[Сотрудница Кафры]";
		mes "Прибыли ли Вы издалека специально";
		mes "чтобы изучить местную культуру?";
		mes "Или, может, Вы случайный прохожий?";
		mes "В любом случае, почему бы Вам не";
		mes "остаться на какое-то время? Воздух";
		mes "здесь вечно наполнен прекрасными";
		mes "ароматами диких цветов.";
		break;
	case 4:// Louyang and Ayothaya specific message (obsolete)
		mes "[Сотрудница Кафры]";
		mes "С услугами Корпорации Кафра";
		mes "Вы всегда недалеко от дома!";
		break;
	case 5:// NPC has it's own welcome message. (Display nothing)
		break;
	}
	next;
	if (.@welcome == 2)// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
		setarray .@K_Menu0$[0], "Склад", "Склад гильдии", "Арендовать телегу", "Телепортироваться", "Отмена";
	else {
		switch(.@menu_num) {
			// Save and Storage only
			case 1:	setarray .@K_Menu0$[0],"Сохранение","Склад","Отмена"; break;
			// Storage only
			case 2:	setarray .@K_Menu0$[0],"Склад","Отмена"; break;
			// No Teleport (Common)
			case 3: setarray .@K_Menu0$[0],"Сохранение","Склад","Арендовать телегу","Другая информация","Отмена"; break;
			// Case 4 is Einbroch no tele message.
			// No save, or teleport. (Common)
			case 5:	setarray .@K_Menu0$[0],"Склад","Арендовать телегу","Другая информация","Отмена"; break;
			// Storage and Check Other Information only.
			case 6:	setarray .@K_Menu0$[0],"Склад","Другая информация","Отмена"; break;
			// Save, Storage, and Pushcart only (Kafra Warehouse)
			case 7:	setarray .@K_Menu0$[0],"Сохранение","Склад","Арендовать телегу","Отмена"; break;
			// Save, Storage, Other Check information. (Turbo track)
			case 8: setarray .@K_Menu0$[0],"Сохранение","Склад","Другая информация","Отмена"; break;
			// No Save (Rune Knight)
			case 9: setarray .@K_Menu0$[0],"Склад","Арендовать телегу","Телепортироваться","Другая информация","Отмена"; break;
			// Storage, Save, and Pushcart (Dewata, reorder of case 7)
			case 10: setarray .@K_Menu0$[0],"Склад","Сохранение","Арендовать телегу","Отмена"; break;
			// Default message (obsolete)
			default: setarray .@K_Menu0$[0],"Сохранение","Склад","Телепортироваться","Арендовать телегу","Другая информация","Отмена"; break;
		}
	}
	.@menu$ = implode(.@K_Menu0$,":");
	while (true) {
		.@j = select(.@menu$) - 1;
		if (.@K_Menu0$[.@j] == "Сохранение")
			return;
		else if (.@K_Menu0$[.@j] == "Склад") {
			if (.@welcome == 2)// Do not charge for Guild Storage
				callfunc "F_KafStor",2,0,0;
			else
				callfunc "F_KafStor",0,getarg(3),.@welcome;
			next;
		}
		else if (.@K_Menu0$[.@j] == "Телепортироваться") {
			if (.@menu_num != 4)// Display Einbroch "No Teleport Service" notice.
				callfunc "F_KafTele",.@welcome;
			else {
				mes "[Сотрудница Кафры]";
				mes "Ввиду ^FF0000Соглашения об ограничении ";
				mes "перевозок^000000 Корпорация Кафра ";
				mes "не обеспечивает телепортации ";
				mes "в Республике Шварцвальт.";
				next;
				mes "[Сотрудница Кафры]";
				mes "Просим Вас пользоваться дирижаблями. ";
				mes "Спасибо за понимание и поддержку.";
			}
			next;
		}
		else if (.@K_Menu0$[.@j] == "Арендовать телегу") {
			if (callfunc("F_KafCart",.@welcome,getarg(4)) == 1)
				next;
		}
		else if (.@K_Menu0$[.@j] == "Другая информация") {
			callfunc "F_KafInfo",getarg(2);
			next;
		}
		else if (.@K_Menu0$[.@j] == "Отмена") {
			callfunc "F_KafEnd",.@welcome,0;
			end;
		}
		else if (.@K_Menu0$[.@j] == "Склад гильдии") {
			callfunc "F_KafStor",1,0;
			next;
		}
	}
}

//===================================================
// Storage Function
//===================================================
function	script	F_KafStor	{
	.@type = getarg(0);
	.@fee = getarg(1);

	// Unable to access Guild Storage (Busy)
	if (.@type == 1) {
		if (guildopenstorage()) {
			mes "[Сотрудница Кафры]";
			mes "Мне очень жаль, но сейчас склад Вашей гильдии используется другим членом гильдии. ";
			mes "Пожалуйста, дождитесь, пока он закончит.";
			close3;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (!callfunc("F_CanOpenStorage")) {
		mes "[Сотрудница Кафры]";
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666П-прррости-и-и...";
			mes "Н-н-ович-чооок....";
			mes "Ввы-ы-ы-ыуч-чить ";
			mes "б-ба-азовый-на-ааавык... ";
			mes "Н-на шшшшеееессссть ";
			mes "у-у-уровне-ей...^000000";
			return;
		}
		mes "Мне очень жаль, но Вам ";
		mes "необходимы Основные умения ";
		mes "как минимум шестого уровня.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (.@type != 2) {
		// Consume "Free Ticket for Kafra Storage" if available.
		if (countitem(7059))
			delitem 7059,1;
		else {
			if (Zeny < .@fee) {
				mes "[Сотрудница Кафры]";
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666Ззееееени...";
					mes "Нужно б-больше ззееениии...!";
					mes "Н-нужно 150... зззени...";
					mes "Кх~! К-кровь...^000000";
					return;
				}
				// Standard Message
				mes "Мне очень жаль, но у Вас ";
				mes "недостаточно зени для оплаты. ";
				mes "Доступ к хранилищу стоит "+.@fee+" зен.";
				return;
			}
			Zeny -= .@fee;
			RESRVPTS += (.@fee/5);
		}
	}

	// Surpress the "End" message if in guild castle.
	if (.@type != 2) {
		// Niflheim specific Message
		if (getarg(2) == 1) {
			percentheal 0,-10;
			mes "[Сотрудница Кафры]";
			for ( .@i = 0; .@i < 5; .@i++ )
				mes "^666666С-спасибо... за... ис-спольз-зование...^000000";
		}
		// Normal message
		else {
			mes "[Сотрудница Кафры]";
			mes "Позвольте открыть Ваше ";
			mes "хранилище. Благодарим за ";
			mes "использование наших услуг.";
		}
	}
	callfunc("F_CheckKafCode");	//check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//===================================================
// Teleport Function
//===================================================
function	script	F_KafTele	{
	mes "[Сотрудница Кафры]";
	mes "Выберите пункт назначения.";
	next;
	.@j = select( implode(@wrpC$,":") ) - 1;
	if (@wrpC$[.@j] == "Отмена")
		return;
	// Consume "Free Ticket for Kafra Transportation" if available.
	// Do not consume if in Guild castle
	if (countitem(7060) > 0 && getarg(0) != 2)
		delitem 7060,1;
	else {
		if (Zeny < @wrpP[.@j]) {
			mes "[Сотрудница Кафры]";
			mes "Прошу прощения, но у вас ";
			mes "не хватает зени для оплаты. ";
			mes "Телепортация в указанный пункт ";
			mes "назначения стоит "+@wrpP[.@j]+" зен.";
			close3;
		}
		Zeny -= @wrpP[.@j];
		RESRVPTS += (@wrpP[.@j]/16);
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "Альдебаран") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "Альберта") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "Комодо") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "Маяк Фарос") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "Геффен") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "Излюд") {
		if (checkre(RENEWAL) == true)
			warp "izlude", 128, 98;
		else
			warp "izlude", 91, 105;
	}
	else if (@wrpD$[.@j] == "Юно") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "Шахты Мьёлльнира") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "Моррок") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "Орочье подземелье") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "Пайон") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "Пронтера") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "Умбала") warp "umbala", 100, 154;
	end;
}


//===================================================
// Cart Function
//===================================================
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes "[Сотрудница Кафры]";
		mes "Прошу прощения, но аренда телеги ";
		if (checkre(RENEWAL) == true)
			mes "возможна только для торговцев и их коллег.";
		else {
			mes "возможна только для торговцев, ";
			mes "кузнецов и алхимиков, а так же их ";
			mes "перерождённых коллег.";
		}
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	else if (checkcart() == true) {
		mes "[Сотрудница Кафры]";
		mes "У вас уже есть телега. ";
		mes "К сожалению, мы не можем ";
		mes "предоставить клиенту более";
		mes "одной телеги единовременно.";
		return 1;
	}
	// Check if the player has the Pushcart skill
	else if (getskilllv("MC_PUSHCART") == 0) {
		mes "[Сотрудница Кафры]";
		mes "Перед арендой телеги Вам необходимо изучить умение обращаться с ней.";
		return 1;
	}
	// Consume "Free Ticket for the Cart Service" if available.
	// Do not consume if in Guild castle.
	if (countitem(7061) > 0 && getarg(0) != 2)
		delitem 7061,1;
	else {
		.@rental_fee = getarg(1);
		mes "[Сотрудница Кафры]";
		mes "Аренда телеги стоит ";
		mes "" + .@rental_fee + " зени.";
		mes "Желаете ли Вы арендовать телегу?";
		next;
		if (select("Взять телегу.","Отмена.") == 2)
			return 0;
		if (Zeny < .@rental_fee) {
			mes "[Сотрудница Кафры]";
			mes "Мне очень жаль, но Ваших средств ";
			mes "недостаточно для оплаты. Аренда ";
			mes "телеги стоит " + .@rental_fee + " зени.";
			return 1;
		}
		Zeny -= .@rental_fee;
		RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//===================================================
// Special Reserve Points Function
//===================================================
function	script	F_KafInfo	{
// Uncomment next line to block Kafra Storage Protection
//	.@block = 1;
	if (getarg(0) == 2)// Port Malaya, Rune Knight
		setarray .@menu$[0], "Проверить накопленные бонусы", "", "", "Отмена";
	else if (.@block)
		setarray .@menu$[0], "Проверить накопленные бонусы", "", "Расположение сотрудниц", "Отмена";
	else
		setarray .@menu$[0], "Проверить накопленные бонусы", "Защитить склад паролем", "Расположение сотрудниц", "Отмена";
	.@menu_list$ = implode(.@menu$,":");
	while (true) {
		.@j = select(.@menu_list$) - 1;
		switch(.@j) {
		case 0:// Check Special Reserve Points.
			mes "[Сотрудница Кафры]";
			mes "Посмотрим...";
			mes "" + strcharinfo(0) + "...";
			mes "Ах, у Вас " +RESRVPTS+ " бонусных очков.";
			next;
			mes "[Сотрудница Кафры]";
			mes "Бонусные очки можно обменять на ";
			mes "специальные призы в Штаб-квартире ";
			mes "Корпорации Кафра в Альдебаране.";
			mes "Бонусы начисляются Вам, когда Вы ";
			mes "пользуетесь нашими услугами.";
			if (getarg(0) == 1)
				return;
			next;
			break;
		case 1:// Storage Password Service
			callfunc("F_SetKafCode","[Сотрудница Кафры]","Корпорация Кафра");
		case 2:// Kafra Employee Locations
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for ( .@i = 0; .@i <= 3; .@i++ )
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			break;
		default:
			return;
		}
	}
}

// === End Function ==================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//  arg(2): used to display the name of the area you're saving in.
//===================================================================
function	script	F_KafEnd	{
	.@type = getarg(0);
	mes "[Сотрудница Кафры]";
	if (getarg(1) == true) {// Save
		mes "Ваша точка сохранения";
		mes "зарегистрирована";
		mes "" + getarg(2) + ".";
		mes "Благодарим за использование ";
		mes "наших услуг.";
	} else if (.@type == 0 || .@type == 5) {// Generic End
		mes "Мы стараемся, чтобы качество наших услуг было наивысшим. Корпорация Кафра надеется, что её работа отвечает всем Вашим нуждам и полностью соответствует Вашему идеалу обслуживания."; 
	} else if (.@type == 1) {// Niflheim End
		percentheal 0,-25;
		mes "^666666Кафффра ннее-";
		mes "умрё-ё-ёт. С-с-с...";
		mes "С-с-ва-амии всегдаа~^000000";
	} else if (.@type == 2) {// RockRidge Save
		mes "Saved.";
		mes "Thank you for your patronage.";
	}
	close3;
}

//===================================================
// Check Storage Password Function
//===================================================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "Введите пароль к Вашему складу:";
	input .@code_;
	if (.@code_ != #kafra_code) {
		dispbottom "Неверный пароль.";
		close3;
	}
	@kafcode_try = 0;
	return;
}

// === Set / Change / Clear Storage Password Function ===
// getarg(0): NPC Name
// getarg(1): Company Name
//=======================================================
function	script	F_SetKafCode	{
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	if (#kafra_code == 0) {
		mes "" + .@comp_name$ + " с гордостью представляет новую функцию:";
		mes "дополнительную защиту склада паролем.";
		next;
		if (select( "Назначить пароль -> 5000 з", "Отмена" ) == 1)
			callsub( S_SET, .@npc_name$, .@comp_name$ );
		close3;
	}
	mes "Ваш склад защищён паролем. Чем могу помочь?";
	next;
	switch( select( "Изменить пароль -> 5000 з","Отказаться от пароля -> 1000 з","Отмена" ) ) {
	case 1:
		mes .@npc_name$;
		mes "Пожалуйста, введите^0000FF старый пароль^000000.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code || .@code != #kafra_code) {
			mes "Неверный пароль. Операция невозможна.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		callsub( S_SET, .@npc_name$, .@comp_name$ );
		break;
	case 2:
		mes .@npc_name$;
		mes "Пожалуйста, введите пароль.";
		.@code = callfunc("F_EntKafCode");
		if (!.@code) {
			mes "Операция не удалась.";
			emotion ET_SCRATCH;
			break;
		}
		next;
		mes .@npc_name$;
		if (Zeny < 1000) {
			mes "У вас не хватает средств.";
			emotion ET_MONEY;
			break;
		}
		Zeny -= 1000;
		if (.@code == #kafra_code) {
			#kafra_code = 0;
			mes "Пароль к Вашему складу успешно изменён.";
			mes "" + .@comp_name$ + " благодарит Вас за сотрудничество.";
			emotion ET_THANKS;
		} else {
			mes "Неверный пароль..";
			mes "Обратите внимание, что мы не можем вернуть плату за услугу. Пожалуйста, будьте внимательны.";
			emotion ET_SORRY;
		}
		break;
	case 3:
		break;
	}
	close3;

S_SET:
	.@npc_name$ = getarg(0);
	.@comp_name$ = getarg(1);

	mes .@npc_name$;
	mes "Введите^FF0000 новый пароль^000000 для склада.";
	.@code = callfunc("F_EntKafCode");
	if (!.@code) {
		mes "Пароль не был изменён.";
		emotion ET_SCRATCH;
		return;
	}
	next;
	mes .@npc_name$;
	if (Zeny < 5000) {
		mes "У вас не хватает средств.";
		emotion ET_MONEY;
		return;
	}
	Zeny -= 5000;
	#kafra_code = .@code;
	mes "Операция назначения пароля успешна.";
	mes "" + .@comp_name$ + " благодарит Вас за использование наших услуг.";
	emotion ET_THANKS;
	return;
}

//===================================================
// Basic Password Validation Function
//===================================================
function	script	F_EntKafCode	{
	mes "Введите число от 1000 до 10000000:";
	@kafcode_try += 1;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "Попытка записать пароль.";
		return 0;
	}
	if (input(.@code_) == 1) {
		mes "Пароль слишком длинный.";
		return 0;
	}
	if (.@code_ < 1000) {
		mes "Пароли длиной менее четырёх (4) цифр не принимаются ввиду ненадёжности.";
		return 0;
	}
	return .@code_;
}

//===================================================
// Warp Points / View Points Function
//===================================================
function	script	F_KafSet	{
	deletearray @wrpC$[0], getarraysize(@wrpC$);
	deletearray @wrpD$[0], getarraysize(@wrpD$);
	deletearray @wrpP$[0], getarraysize(@wrpP$);
	deletearray @viewpX[0], getarraysize(@viewpX);
	deletearray @viewpY[0], getarraysize(@viewpY);

	.@map$ = strnpcinfo(4);
	if (.@map$ == "prontera") {
		setarray @wrpD$[0], "Излюд", "Геффен", "Пайон", "Моррок", "Орочье подземелье", "Альберта";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1700, 1800;
		setarray @viewpX[0], 151, 29, 282, 152;
		setarray @viewpY[0], 29, 207, 200, 326;
	} else if (.@map$ == "alberta") {
		setarray @wrpP[0], 1200, 1800, 1800;
		setarray @wrpD$[0], "Пайон", "Моррок", "Пронтера";
		setarray @viewpX[0], 28, 113, 0, 0;
		setarray @viewpY[0], 229, 60, 0, 0;
	} else if (.@map$ == "aldebaran") {
		setarray @wrpP[0], 1200, 1200, 1800, 1700;
		setarray @wrpD$[0], "Геффен", "Юно" , "Излюд", "Шахты Мьёлльнира";
	} else if (.@map$ == "comodo" ) {
		setarray @wrpD$[0], "Моррок", "Маяк Фарос", "Умбала";
		setarray @wrpP[0], 1800, 1200, 1800;
	} else if (.@map$ == "cmd_fild07") {
		setarray @wrpD$[0], "Комодо", "Моррок";
		setarray @wrpP[0], 1200, 1200;
	} else if (.@map$ == "geffen") {
		setarray @wrpD$[0], "Пронтера", "Альдебаран", "Орочье подземелье", "Шахты Мьёлльнира";
		setarray @wrpP[0], 1200, 1200, 1700, 1700;
		setarray @viewpX[0], 120, 203;
		setarray @viewpY[0], 62, 123;
	} else if (.@map$ == "izlude") {
		setarray @wrpD$[0], "Геффен", "Пайон", "Моррок", "Альдебаран";
		setarray @wrpP[0], 1200, 1200, 1200, 1800;
	} else if (.@map$ == "morocc") {
		setarray @wrpD$[0], "Пронтера", "Пайон", "Альберта", "Комодо", "Маяк Фарос";
		setarray @wrpP[0], 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX[0], 156, 163, 28, 292;
		setarray @viewpY[0], 97, 260, 167, 211;
	} else if (.@map$ == "umbala") {
		setarray @wrpD$[0], "Комодо";
		setarray @wrpP[0], 1800;
	} else if (.@map$ == "payon") {
		setarray @wrpD$[0], "Пронтера", "Альберта", "Моррок";
		setarray @wrpP[0], 1200, 1200, 1200;
	} else if (.@map$ == "yuno") {
		setarray @wrpD$[0], "Альдебаран";
		setarray @wrpP[0], 1200;
		setarray @viewpX[0], 328, 278, 153, 0;
		setarray @viewpY[0], 108, 221, 187, 0;
	} else if (.@map$ == "job3_rune01") {
		setarray @wrpD$[0], "Излюд", "Геффен", "Пайон", "Моррок", "Альберта";
		setarray @wrpP[0], 600, 1200, 1200, 1200, 1800;
	}

	.@warp_size = getarraysize(@wrpD$);

	// VIP pricing varies across the regional servers; this is most practical.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		for ( .@i = 0; .@i < .@warp_size; .@i++ )
			@wrpP[.@i] *= 2;
	}

	for ( .@i = 0; .@i < .@warp_size; .@i++ )
		@wrpC$[.@i] = @wrpD$[.@i] + " -> " + @wrpP[.@i] + " з";
	@wrpC$[ .@warp_size ] = "Отмена";
	return;
}
